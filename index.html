<!DOCTYPE html>
<html>
<head>
  <title>Flora</title>
  <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/github-fork-ribbon-css/0.2.0/gh-fork-ribbon.min.css" />
<!--[if lt IE 9]>
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/github-fork-ribbon-css/0.2.0/gh-fork-ribbon.ie.min.css" />
<![endif]-->
<!-- Most of the css style comes from a three.js example at https://github.com/mrdoob/three.js/blob/master/examples/misc_controls_pointerlock.html -->
  <style>
    body { margin: 0; }
    canvas { width: 100%; height: 100% }
    html, body {
      width: 100%;
      height: 100%;
    }
    body {
      background-color: #ffffff;
      margin: 0;
      overflow: hidden;
      font-family: arial;
    }
    #blocker {
      position: absolute;
      width: 100%;
      height: 100%;
      background-color: rgba(0,0,0,0.5);
    }
    #instructions {
      width: 100%;
      height: 100%;
      display: -webkit-box;
      display: -moz-box;
      display: box;
      -webkit-box-orient: horizontal;
      -moz-box-orient: horizontal;
      box-orient: horizontal;
      -webkit-box-pack: center;
      -moz-box-pack: center;
      box-pack: center;
      -webkit-box-align: center;
      -moz-box-align: center;
      box-align: center;
      color: #ffffff;
      text-align: center;
      cursor: pointer;
    }
  </style>
</head>
<body>
<div id="container"></div>
<div id="blocker">

  <div id="instructions">
    <span style="font-size:40px">Click to play</span>
    <br />
    (W, A, S, D = Move, MOUSE = Look around)
  </div>

</div>
<div id="notSupported" style="display:none">Sorry your graphics card + browser does not support hardware instancing</div>
<script src="datgui/dat.gui.js"></script>
<script src="threejs/three.min.js"></script>
<script src="threejs/OBJLoader.js"></script>
<script src="threejs/PointerLockControls.js"></script>
<script src="statsjs/Stats.js"></script>
<script src="controls.js"></script>

<script id="vertexShader" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec3 offset;
attribute vec3 color;

attribute float stiffness;
// Angle around vertical axis (yaw). Lies between 0 and 2*PI
attribute float angle;
// scale the height by this number
attribute float heightCoefficient;
// scale the width by this number
attribute float widthCoefficient;
// the tilt of the tip of the grass piece against the vertical
attribute float curve;
// multiply curve by this number
uniform float curveFactor;
// how spread out the pieces are in the field
uniform float spreadFactor;
// the original spread of the pieces, i.e., the length of the diameter of
// the circle on which the pieces are
uniform float spread;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

varying vec3 vColor;

// Global time
uniform float time;
// Startinf time of the wind blow
uniform float windStartTime;
// The angle in which the wind blows. It is assumed to be between 0 and 2*PI
uniform float windAngle;
// The players position (the center of the circle) when the wind started
uniform vec3 windStart;
// The speed at which the wind travels through the field
uniform float windSpeed;
// The time it takes for the wind to fully deflect a grass piece
uniform float windRiseTime;
// The time it takes for the grass piece to go back to natural position
uniform float windSettlingTime;
// The position of the player (camera) in the grass field
uniform vec3 playerPosition;

vec3 viewOffset;

const float PI = 3.141592653589793238462643383;
// The original height in the y direction of a grass piece
const float unitHeight = 3.66;

mat3 rotateAroundY(float angle)
{
    float s = sin(angle);
    float c = cos(angle);

    return mat3(c ,0 ,-s,
                0, 1 , 0,
                s, 0 , c);
}

/*
 * Return the difference between the grass rotation angle and the
 * wind angle. The returned value should be between -2*PI and 0
 */
float angleDiff() {
  float diff = windAngle - angle;
  if (diff > 0.0) {
    return diff - 2.0*PI;
  } else {
    return diff;
  }
}

/*
 * Return a value between -1 and 1 which represents the strength of wind.
 * A value near zero means that the wind has weak effects on the grass piece,
 * while a value near -1 or 1 means the wind has strong effects. The sign of
 * the returned value means the effects relative to the pieces curvature, i.e.,
 * if the wind blows agains the curve, it will have a negative value.
 */
float wind()
{
  // the virtual distance of this vertex from the wind wave at the moment
  // when the wind started
  float virtualDistance = (cos(windAngle)*(viewOffset.z-windStart.z) +
                           sin(windAngle)*(viewOffset.x-windStart.x))-spread/2.0;
  // the relative position-dependent time since the wind has started
  float t = time-windStartTime+(virtualDistance/windSpeed);
  // helper values for the gaussian functions: spread the settlingSpread
  // will be the times for which the half-gaussian will be above the
  // 10% of its maximal value.
  float spread = windRiseTime/2.145965;
  float settlingSpread = windSettlingTime/2.145965;
  // the amount of time for which the piece should be deflected at its maximum
  // for now we dont use it, so it is 0
  float len = 0.0;
  // the difference between the pieces angle and the wind angle.
  // this value can only be between -2*PI and 0.
  float diff = angleDiff();
  // -1.0 if the wind blows agains the pieces curvature, 1.0 otherwise
  float reverse = 1.0;
  if (diff < -PI/2.0 && diff > -3.0*PI/2.0) {
    reverse = -1.0;
  }
  // The transitional stage is governed by one half of a Gaussian function,
  // while the maximum deflection stage is constant (-1.0 or 1.0)
  if (t < windRiseTime) {
    // We need to work with abs because pow result is undefined if the
    // first argument is a negative value
    return reverse*exp(-pow(abs(t-windRiseTime),2.0)/(2.0*pow(spread,2.0)));
  } else if (t > (windRiseTime+len)) {
    float x = t-windRiseTime-len;
    // Do a little more than a half cosine in the settling phase
    float f = 0.55*2.0*PI/windSettlingTime;
    return reverse*cos(f*x)*exp(-pow(x,2.0)/(2.0*pow(settlingSpread,2.0)));
  } else {
    return reverse*1.0;
  }
}

/*
 * Return the angle to which the wind in its strongest state will rotate
 * the tip of the grass piece.
 */
float rotateInWind()
{
  float diff = angleDiff();
  // The wind never rotates more than 90 degrees
  if (diff < -PI/2.0 && diff > -PI) {
    diff = PI+diff;
  } else if (diff < -PI && diff > -3.0*PI/2.0) {
    diff = PI+diff;
  } else if (diff < -3.0*PI/2.0) {
    diff = 2.0*PI+diff;
  }
  // The lowest height on the grass piece at which it can twist
  float twistHeight = unitHeight*0.3;
  // The amount that this vertex will twist
  float twistCoefficient = (position.y-twistHeight)/unitHeight;

  if (position.y < twistHeight) {
    return 0.0;
  } else if (twistCoefficient < 1.0) {
    return diff*twistCoefficient;
  } else {
    return diff;
  }

}

float round(float x) {
  return floor(0.5+x);
}

/*
 * Offset the grass piece with integer multiples of spread in the x and z directions
 * to be in the near the players current position.
 */
void calculateViewOffset() {
  vec3 playerPositionInPlane = vec3(playerPosition.x,0,playerPosition.z);
  vec3 diff = playerPositionInPlane - offset;
  viewOffset = offset;
  float dlen = length(diff);
  if (dlen > spread/2.0) {
    viewOffset.x += round(diff.x/spread)*spread;
    viewOffset.z += round(diff.z/spread)*spread;
  }
}

void main()
{
  vec3 vPosition = position;
  vPosition.x *= widthCoefficient;
  // the length (height) of the scaled piece
  float len = unitHeight*heightCoefficient;
  // wind is zero degrees
  float windAngle = angle;

  calculateViewOffset();
  float windStrength = wind();
  // the imaginary radius of the arc

  float freq = (2.0/stiffness) + 2.0;
  float amplitude = (0.3*(PI/180.0));
  // the imaginary radius of the arc
  float r = len/(curve*curveFactor+0.5*windStrength+amplitude*sin(freq*time));
  // the tilt of the piece corresponding to the vPosition.y parameter
  float tilt = vPosition.y*heightCoefficient/r;
  vPosition.z = cos(tilt)*r-r;
  vPosition.y = sin(tilt)*r;

  // rotate around y axis
  vPosition = rotateAroundY(angle+rotateInWind()*abs(windStrength))*vPosition;


  gl_Position = projectionMatrix * modelViewMatrix * vec4(vPosition+viewOffset*spreadFactor,1.0);
  vColor = color;
}

</script>

<script id="fragmentShader" type="x-shader/x-fragment">
precision highp float;
uniform vec3 fogColor;
uniform float fogNear;
uniform float fogFar;
varying vec3 vColor;
void main()
{
    gl_FragColor = vec4(vColor, 1.0);
    float depth = gl_FragCoord.z / gl_FragCoord.w;
    float fogFactor = smoothstep( fogNear, fogFar, depth );
    gl_FragColor.rgb = mix( gl_FragColor.rgb, fogColor, fogFactor );
}
</script>

<script src="main.js"></script>
<a class="github-fork-ribbon right-bottom fixed" href="http://github.com/petoalbert/flora" title="Fork me on GitHub">Fork me on GitHub</a>
</body>
</html>
